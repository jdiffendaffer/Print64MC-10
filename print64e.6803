;**************************************************
;* File: print64.6803
;**************************************************
;* Copyright (c) 2008-2015 James Diffendaffer
;*
;*  I hereby grant an unrestricted license to use this code as long
;*  as you give me credit for my work.
;**************************************************
;* Description:
;*  Emulates a 64 character by 24 line text display using 
;*  4 bit wide characters on a 256 * 192 graphics display.
;*  This version is for the Tandy MC-10 and 6803 CPU
;*
;*  Contains functions to print a string at the current screen location,
;*  print strings @ a specific screen location, line wrapping, scrolling,
;*  clear screen, the character generator and a font.
;*  Characters are stored with identical data in the left and right nibble
;*  to make rendering faster and several loops have been unrolled for speed.
;*  
;*  Loosly based on my VZ200 Z80 code's logic
;**************************************************
;* Version history
;*  Version  Date               Description
;*     2.09   Aug  30, 2018      Optimized 2 char print by using A and B, changed code from 7 to 8 byte font
;*     2.08   Oct  12, 2016      Fixed f character.
;*     2.07   Apr  11, 2016      Scroll code is generated on startup, uses completely unrolled LDD STD code
;*     2.06   Oct   6, 2015      Initial support for printing 2 characters at once when possible
;*     2.05   Oct   5, 2015      Added ability to invert text, cut some clock cycles from print, fixed a bug in scroll 
;*     2.04   Sept 30, 2015      Changed text rendering code to use EOR, saves 1 instruction per pixel height of character
;*     2.03   Sept 28, 2015      Added documentation,
;*     2.02   Sept 26, 2015      Optimizations to print64, changed some comments
;*	   2.01   Sept  9, 2015      Optimized print routine, removed some unneeded code, corrected some comments
;*     2.00   Sept  5, 2015      refactored for greater speed.    
;*	                             Moved line wrap and scrolling to print function to match Z80 functionality
;*                               Font changed to use mirroed data in each nibble of the data to eliminate 
;*                               data shifting.
;*                               Older version information removed with refactoring.  
;**************************************************

;include MOTO.h
;defined for tasm.  tasm normally doesn't support motorola directives
.MSFIRST        			; Most Significant byte first

#define EQU     .EQU
#define ORG     .ORG
#define RMB     .BLOCK
#define FCB     .BYTE
#define FCC     .TEXT
#define FDB     .WORD
#define END		.END

#define equ     .EQU
#define org     .ORG
#define rmb     .BLOCK
#define fcb     .BYTE
#define fcc     .TEXT
#define fdb     .WORD
#define	end		.END


;**************************************************
;MC-10 ROM entry vector addresses
;**************************************************
POLCAT	equ		$FFDC		; Read the keyboard
CHROUT	equ		$FFDE		; Output a character to the current device
CRSDON	equ		$FFE0		; Turn on the Cassette
BLKIN	equ		$FFE2		; Write a block of data to the cassette
BLKOUT	equ		$FFE4		; Read a block of data from the cassette
SNDOUT	equ		$FFE6		; Sound Output
WRTLDR	equ		$FFE8		; Write Leader
GIVABF	equ		$FFEA		; Convert 2 bytes to Basic
INTCNV	equ		$FFEC		; Convert BASIC to 2 bytes


;*************************************
; 6803 timer hardware
;*************************************
TCSR	EQU		$0008		; Timer Control Status Register
TIMER	EQU		$0009		; Counter
OCR		EQU		$000B		; Output Compare Register
OCV		EQU		$4206		; Output Compare interrupt Vector


;**************************************************
; MC-10 Graphics hardware definitions
;**************************************************
#define	screen	$4000		; address of MC-10 screen memory
#define	vdgport	$BFFF		; I/O address to set the VDG mode
#define	RG6		%00111100	; RG6 graphics mode bits
#define	CSS		%01000000	; color set select bit

;#define	sourceaddress
;**************************************************
;* screen parameters
;*
;*  Define the graphics mode you are going to use as 1
;*  This is used to set up all the screen parameters used by
;*  the graphics routines so they will work in your 
;*  chosen mode. 
;**************************************************
#define GFX_RG6	1			; Set the mode we will use here


;**************************************************
;* sets up the screen parameters based on define above
;**************************************************
#ifdef GFX_RG6						; parameters for RG6
#define	RGMODE 1
ScreenWidth		equ	256
ScreenHeight	equ	192
BytesPerLine	equ	((ScreenWidth)/8)
#endif

MAX_X			equ	ScreenWidth-1
MAX_Y			equ	ScreenHeight-1

;**************************************************
;page zero variables
;**************************************************
			org $00A0				; start of Page 0 variables

temp0		rmb	2					; temporary storage location.  used for transfering D to X or X to D etc...
temp1		rmb	2					; temporary storage location.


;OE	equ	FGCOLOR+1
charptr		rmb	2
fscreen		rmb	2
fcount		rmb	1

;**************************************************
; location to temporarily hold the stack pointer
; when it is used as a pointer register
;**************************************************
stacktmp	rmb	2

;**************************************************
; graphics text routine variables and macros
;**************************************************
row			rmb	1
col			rmb	1
BGColor		rmb 2
leftchar	rmb	2
rightchar	rmb 2

#DEFINE PRINTAT(loc,str) ldd loc \ ldx #str \ jsr print_at
#DEFINE PRINT(str) ldx #str \ jsr print
#DEFINE SetVDG(setting)	ldaa #setting \ staa vdgport
;#DEFINE SetBGColor(setting)	ldaa #setting \ staa BGColor \ staa BGColor+1

;**************************************************
;**************************************************
;**************************************************

START	org	$8000					;where to store out program


;**************************************************
;* NAME: textdemo
;**************************************************
;* DESCRIPTION:
;*  a short program to demonstrate the use of 
;*  routines in the graphics library
;**************************************************
Textdemo:
	; save the registers we will modify
;	psha
;	pshb
;	pshx

	ldd		#$FFFF				; row, col
;	ldd		#$0000				; row, col
	std		BGColor				; set the background color
	jsr		invert_mem			; inverse font
	jsr		cls

;	ldaa	#RG6	;+CSS			; the graphics and color mode
;	staa	vdgport					; set it

	SetVDG(RG6)					; set the graphics mode
	ldd		#0
	std		row					; clear row and column, they are consecutive bytes on the direct page

; 64 character/line demo, prints entire character set
	ldx		#String				; point to our string buffer
	ldaa	#' '				; the first character in the font
lllm:
	staa	,x					; save it to the string
	inx							; next string address
	inca						; next character
;	cmpa	#'~'+1				; are we past the last character in the font?
	cmpa	#'~'+16				; are we past the last character in the font?
	bne		lllm
	ldaa	#0					; zero terminate the string
	staa	,x
start:
	PRINT(String)
	bra		start

	rts


;**************************************************
;* NAME: cls
;**************************************************
;* DESCRIPTION:
;*  Clears the screen memory to zeros
;*
;* The stack pointer is used as 2nd pointer register
;* but interrupts must be disabled to do this.
;* We could just use X as a pointer but PSHX is faster
;* than the alternative code
;**************************************************
cls
	pshx
	pshb
;	psha
	
	tsx							; copy stack pointer to x
	stx		stacktmp
;	wai							;<- uncomment if you want to make sure cls takes place between interrupts
	sei							;disable interrupts
	lds		#screen+(BytesPerLine*(ScreenHeight))-1	;last screen address.
;	txs							;point S to screen top
	ldx		BGColor				;clear the screen to the background color
	ldab	#((BytesPerLine*(ScreenHeight))/32)		;number of loops to clear screen
_clsloop:
	pshx						; Partially unrolled loop clears at least an entire screen row each pass
	pshx						;  lets us use an 8 bit loop counter even in RG6
	pshx						;  so we don't have to dec and test LSB and MSB of loop counter (there is no DECD instruction)
	pshx						;  32 bytes / pass = first power of 2 with loop counter below 256 (fits in 1 byte)
	pshx						; Fewer loops also requires fewer loop instructions to execute
	pshx						;  and saves a lot of clock cycles.
	pshx
	pshx
	pshx
	pshx
	pshx
	pshx
	pshx
	pshx
	pshx
	pshx
	decb						;decrement loop counter
	bne		_clsloop			;branch until it equals zero
	ldx		stacktmp
	txs							;restore the stack pointer
	cli							;enable interrupts

;	pula
	pulb
	pulx

	rts


;**************************************************
;* Description:
;*  scroll - scroll the 24 line display and clear the last line
;*  InitScroll - Generates the code for the unrolled loops 
;*     must be called before you can call scroll as there is no code at the destination addresses on load.
;**************************************************
;68hc11 version
;#define MScroll(A)	ldx A+30,y \ pshx \ ldx A+28,y \ pshx \ ldx A+26,y \ pshx \ ldx A+24,y \ pshx \ ldx A+22,y \ pshx \ ldx A+20,y \ pshx \ ldx A+18,y \ pshx \ ldx A+16,y \ pshx \ ldx A+14,y \ pshx \ ldx A+12,y \ pshx \ ldx A+10,y \ pshx \ ldx A+8,y \ pshx \ ldx A+6,y \ pshx \ ldx A+4,y \ pshx \ ldx A+2,y \ pshx \ ldx A,y \ pshx
#define MScroll(A)	ldd A+30,x \ pshb \ psha \ ldd A+28,x \ pshb \ psha \ ldd A+26,x \ pshb \ psha \ ldd A+24,x \ pshb \ psha \ ldd A+22,x \ pshb \ psha \ ldd A+20,x \ pshb \ psha \ ldd A+18,x \ pshb \ psha \ ldd A+16,x \ pshb \ psha \ ldd A+14,x \ pshb \ psha \ ldd A+12,x \ pshb \ psha \ ldd A+10,x \ pshb \ psha \ ldd A+8,x \ pshb \ psha \ ldd A+6,x \ pshb \ psha \ ldd A+4,x \ pshb \ psha \ ldd A+2,x \ pshb \ psha \ ldd A,x \ pshb \ psha
#define MScrollB(A)	ldd A+30,x \ pshb \ psha \ ldd A+28,x \ pshb \ psha \ ldd A+26,x \ pshb \ psha \ ldd A+24,x \ pshb \ psha \ ldd A+22,x \ pshb \ psha \ ldd A+20,x \ pshb \ psha \ ldd A+18,x \ pshb \ psha \ ldd A+16,x \ pshb \ psha \ ldd A+14,x \ pshb \ psha \ ldd A+12,x \ pshb \ psha \ ldd A+10,x \ pshb \ psha \ ldd A+8,x \ pshb \ psha \ ldd A+6,x \ pshb \ psha \ ldd A+4,x \ pshb \ psha \ ldd A+2,x \ sei \ pshb \ psha \ ldd A,x \ pshb \ psha
#define ClLine    pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx
#define ClLine2   pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ pshx \ sei \ pshx \ pshx \ pshx \ pshx

.MODULE Scroll
scroll:
	ldaa	#23
;	ldaa	#15
	staa	temp0					; loop counter
	ldx		#screen+(BytesPerLine*8)	; 
	sts		stacktmp					; save the stack pointer
;	lds		#screen+(BytesPerLine*8)-1	; character line +1
	txs
	;the scroll section
_scrolla:
	MScroll(224)					; for scrolling all lines instead of erasing one.
	MScroll(192)
	MScroll(160)
	MScroll(128)
	MScroll(96)
	MScroll(64)
	MScroll(32)
	MScrollB(0)
	
	dec		temp0					; decrement our loop counter
	ble		nxtscroll				; exit if we are done

	ldab	#255					; adjust x for the next text row
	abx
	inx
	txs

	cli

 	jmp		_scrolla				; keep going until we've scrolled every text row

nxtscroll:	
	;clear the bottom charater row
	lds		#screen+(BytesPerLine*(ScreenHeight))-1	;last screen address.
	cli
	ldx		BGColor					; clear x for use in the macro
	ClLine							; clear the top line of the text row
	ClLine							; next one down
	ClLine							; and so on
	ClLine
	ClLine
	ClLine
	ClLine
	ClLine2							; last one, use a different macro to disable interrupts for the last 8 bytes
									; we don't want an interrupt to overwrite memory below this line

	;restore stack & interrupts
	lds	stacktmp				; restore the stack pointer
	cli							; enable interrupts

	rts							; done

;.MODULE Scroll
;#define CodeAddress1	$04CA	;MCX128 configuration
;#define LastAddress1	$3FFF
;#define	CodeAddress2	$C000
;#define LastAddress2	$C763
;#define LastAddress3	LastAddress2+18
;
;scroll:
;	; < if using paged RAM, set first page here
;	LDAA	#$01
;	STAA	$BF00				; set bank 1 for MCX-128 page 1
;	JSR		CodeAddress1
;	; < if using paged RAM, set second page here if needed
;	; already set
;	JSR		CodeAddress2
;	
;	; < restore normal memory page here
;	LDAA	#$00
;	STAA	$BF00				; set bank 0 for MCX-128 page 0
;	rts							; done
	
;; This code generates the unrolled scroll code.
;InitScroll:
;;initialize the pointers to generate the scroll code
;	LDD		$screen+256			; get the screen address + 256
;	STD		SourceAddress		; save it as the source address
;	SUBD	#$256				; Subtract 256, the number of bytes between source and destination
;	STD		DestAddress			; save it as the destination address
;
;	LDD		#LastAddress1		; the address we want to stop at
;	STD		LastAddress			; set it
;	LDX		#CodeAddres1		; put the address of where we want to write the code in X
;	; < if using paged RAM, set first page here
;	LDAA	#$01
;	STAA	$BF00				; set bank 1 for MCX-128 page 1
;	
;	JSR		ScrollGen1			; generate the 1st block of code
;
;	LDD		#LastAddress2		; the address we want to stop at
;	STD		LastAddress			; set it
;	LDX		#CodeAddress2		; set up next address to write code to in X
;	; < if using paged RAM, set second page here if needed
;	; already set
;	JSR		ScrollGen1			; generate the 2nd block of code
;
;	LDD		#LastAddress3		; the address we want to stop at
;	STD		LastAddress			; set it
;	JSR		ScrollGen2			; generate code to clear the last line
;	; < restore normal memory page here
;	LDAA	#$00
;	STAA	$BF00				; set bank 0 for MCX-128 page 0
;;exit initialization here
;	RTS
		
	
;; generate code
;ScrollGen1:
;	; generate the code to load 2 bytes
;	LDAA	#$FC				; opcode for LDD ####
;	STAA	,X					; save it at current address
;	INX							; point to next address
;	LDD		SourceAddress		; get current screen source address
;	STD		,X					; write it
;	INX							; update the memory pointer
;	INX
;	ADDD	#$0002				; Add two to the Source Address
;	STD		Source Address		; Update it
;	
;	;generate the code to save 2 bytes
;	LDAA	#$FD				; opcode for STD ####
;	STAA	,X					; save it at current address
;	INX							; point to the next address
;	LDD		DestAddress			; get current screen destination address
;	STD		,X					; write it to the code
;	INX							; update the memory pointer
;	INX
;	ADDD	#$0002				; Update the destination address
;	STD		DestAddress			; 
;	
;	;bottom of the loop
;	CPX		LastAddress			; Does X point to the last memory address in this block?
;	BNE		ScrollGen1
;	
;	;finish block of code with RTS
;	LDAA	#$39				; opcode for RTS
;	STAA	,X					; save it at the current address
;
;	RTS							; return when done
;
;
;	; generate code to clear last line (256 characters)
;	; this should overwrite the RTS in the last scroll so the last line is automatically cleared
;	; without an additional JSR.
;	
;	; Generate code to clear the last printed line.  
;	; Point the stack to the end of the screen so we can use PSHX to clear most of it. 
;	; Output 118 PSHX instructions and 10 STD instructions.
;	; 1 text line is 256 bytes, we write 2 bytes at a time so divide by 2
;	; 256/2=128.  The 6803 stack is 7 bytes long, 68hc11 is 9, + 1 since we write 2 bytes at a time
;	; Since the code generates 2 PSHX opcodes at a time we have to round up to the next number
;	; divisible by 4 which is 12
;	; 128 - 12 = 116
;	; Restore stack pointer
;	; clear D
;	; output 6 STD instructions
;	; The standard stack order (low to high in memory) by an interrupt (7 bytes)
;	; add 2 bytes for IYL and IYH for 68hc11 compatability, + 1 to make it an even number
;	; PCL, PCH, IYL, IYH, IXL, IXH, ACCA, ACCB, CCR
;	; then restore the stack pointer, clear D and store D at remaining addresses
;ScrollGen2:	
;	; code to load X with 0
;	LDAA	#$FE				; LDX #$#### opcode
;	STAA	,X
;	INX
;	LDD		#$0000				; loading X with zero
;	STD		,X
;	INX
;	INX
;	
;	; code to save the stack pointer
;	LDAA	#$BF				; opcode for STS ####
;	STAA	,X					
;	INX
;	LDD		#StackTemp			; temporary address to store stack pointer
;	STD		,X
;	INX
;	INX
;	
;	; code to set the stack pointer to point to the end of the screen
;	LDAA	#$BE				; LDS #$#### opcode
;	STAA	,X
;	INX
;	LDD		#screen+(BytesPerLine*(ScreenHeight))-1	;last screen address.
;	STD		,X
;	INX
;	INX
;
;	; generate block of PSHX instructions
;	LDD		#$3C3C				; two PSHX opcodes
;clearcode1:
;	STD		,X					; output two opcodes
;	INX							; next address
;	INX
;	CPX		#LastAddress2
;	BNE		clearcode1			; keep going until we are done
;
;	;generate code to restore stack pointer
;	LDAA	#$BE				; LDS #$#### opcode
;	STAA	,X
;	INX
;	LDD		#StackTemp
;	STD		,X
;	INX
;	INX
;	
;	; clear D and generate block of STD instructions
;	LDD		#screen+(BytesPerLine*8*23)		;starting address of final text row
;	STD		DestAddress
;	LDAA	#$FC				;LDD #$#### opcode
;	STAA	,X
;	INX
;	LDD		#$0000				; zero (to clear bytes with PSHX)
;	STD		,X
;	INX
;	INX
;
;	;now generate the 6 STD #### instructions
;clearcode2:
;	LDAA	#$FD				;STD #### opcode
;	STAA	,X
;	INX
;	LDD		DestAddress
;	STD		,X
;	INX
;	INX
;	ADDD	#$2
;	STD		DestAddress
;	; bottom of clearcode2 loop
;	CPX		#LastAddress3
;	BNE		clearcode2
;
;	;finish block of code with an RTS
;	LDAA	#$39				; opcode for RTS
;	STAA	,X					; save it at the current address
;	
;	RTS

;**************************************************
;* invert_mem
;**************************************************
;*  nope length contains the length
;**************************************************
invert_mem:
	ldx		#leftfont
nxtaddr:
	ldd		,x							; get original byte
	eora	#$F0						; invert it
	eorb	#$F0						; invert it
	std		,x							; write it back
	inx
	inx
	cpx		#rightfont
	bmi		nxtaddr
nxtaddr2:
	ldd		,x							; get original byte
	eora	#$0F						; invert it
	eorb	#$0F						; invert it
	std		,x							; write it back
	inx
	inx
	cpx		#String
	bmi		nxtaddr2
	
	rts

	
;**************************************************
;* print_at
;**************************************************
;* Prints a string at a screen location
;* works similar to print@ in BASIC
;*  d contains @ location
;*  x contains string pointer
;* falls through to _print
;**************************************************
;.MODULE Print_AT
print_at:
	stab	temp0				;save it
	andb	#(BytesPerLine*2)-1	;%00111111	;mask of row bits
	stab	col					;save the column
	ldab	temp0				;restore it
	andb	#254-(BytesPerLine*2)	;%11000000	;mask off column bits
	lsld						;rotate bits into a
	lsld
	staa	row					;save row

	
;**************************************************
;* print
;**************************************************
;* Prints a zero terminated string at the current x,y location
;*  x contains the string pointer
;**************************************************
.MODULE Print
print:
	ldaa	col
	lsra
	bcs		prn1				; print single char if we are on the right nibble
	
prn4:							; see if we can print 2 characters at once
	ldd		,x					; get next 2 characters from the string
	cmpa	#0
	beq		_printexit			; exit if first char is a string terminator

	cmpb	#0
	beq		prn2				; print single char if 2nd char a string terminator

	; print 2 characters
	pshx
	jsr		print_642
	pulx
	; inc string pointer and column an extra time since we printed 2 characters
	; since we know we were on a byte boundary we know we won't be going more than one column past the end of the line
	inx
	inc		col
	bra		prn3				; continue with normal return from printing

prn1:
	ldaa	,x
	cmpa	#0
	beq		_printexit			; exit if it's a zero
prn2:
	pshx
	jsr		print_64			; print the character
	pulx						; restore x
prn3:
	inx							; increment the string pointer x
	;update column, row, and scroll when needed
	inc		col					; next column
	ldaa	col
	cmpa	#(BytesPerLine*2)	; 64 chsrs / line (0-63)
	bne		prn4				; is column past the end of a line?

	clr		col					; set column to zero
	inc		row					; increment row
	ldaa	row
	cmpa	#(ScreenHeight/8)	; 24 lines (0-23)
;	cmpa	#16					; 16 lines (0-23) *for debugging*
	bne		prn4
	
	dec		row
	pshx	; save x
	jsr		scroll				; scroll the screen
	pulx						; restore x
	
	bra		prn4				; keep printing
_printexit:						; we are done
	rts
	
	
;**************************************************
;* NAME: scroll
	
;**************************************************
; NAME: print_64
;**************************************************
;* Description:
;*  64 Column text display driver
;*  Routine does not print at pixel X,Y but
;*  prints at a character position.
;*  Once a string printing routine is written, 
;*  some of the code may be moved to that.
;**************************************************
.MODULE Print_64
print_64:
	ldab	#8					; 7 bytes per character
	mul							; multiply them
	std		temp0				; save font offset

	ldd		row					; screen offset
	lsrb
	bcs		rightnibble

;**************************************************
;* left nibble
;**************************************************
	addd	#screen				; 7 row font data, top line of character is always blank so skip it
	std		SCRNl+1				; transfer to x for our screen pointer
SCRNl:
	ldx		#0000				; self modifying code

	ldd		#leftfont-257
	addd	temp0
	std		CHARPTRl+1			; save to character pointer

	sts		stacktmp			; save the stack pointer
	sei							; disable interrupts

CHARPTRl:						; for self modifying code
	lds		#0000				; point the stack to the current character
;	lds		charptr				; point the stack to the current character

;start printing
	pula
	pulb						; get byte of font and point to next font data
	eora	0,x
	eorb	32,x				; read byte at destination
	staa	,x					; the top row of the font is always blank
	stab	32,x

	pula						; get byte of font and point to next font data
	pulb						; get byte of font and point to next font data
	eora	64,x				; read byte at destination
	eorb	96,x				; read byte at destination
	staa	64,x
	stab	96,x

	pula						; get byte of font and point to next font data
	pulb						; get byte of font and point to next font data
	eora	128,x				; read byte at destination
	eorb	160,x				; read byte at destination
	staa	128,x
	stab	160,x

	pula						; get byte of font and point to next font data
	pulb						; get byte of font and point to next font data
	;restore stack pointer and interrupts
	lds		stacktmp			; restore the stack pointer
	cli							; enable interrupts
	eora	192,x				; read byte at destination
	eorb	224,x				; read byte at destination
	staa	192,x
	stab	224,x


	rts							; return
	

;**************************************************
;* right nibble
;**************************************************
rightnibble:
	addd	#screen				; 8 row font data, top line of character is always blank so skip it
	std		SCRNr+1				; transfer to x for our screen pointer
SCRNr:
	ldx		#0000				; self modifying code

	ldd		#rightfont-257
	addd	temp0
	std		CHARPTRr+1			; save to character pointer

	sts		stacktmp			; save the stack pointer
	sei							; disable interrupts

CHARPTRr:						; for self modifying code
	lds		#0000				; point the stack to the current character
;	lds		charptr				; point the stack to the current character

;start printing
	pula						; get byte of font and point to next font data
	pulb						; get byte of font and point to next font data
	eora	,x					; read byte at destination
	eorb	32,x				; read byte at destination
	staa	,x					; the top row of the font is always blank
	stab	32,x

	pula						; get byte of font and point to next font data
	pulb						; get byte of font and point to next font data
	eora	64,x				; read byte at destination
	eorb	96,x				; read byte at destination
	staa	64,x
	stab	96,x

	pula						; get byte of font and point to next font data
	pulb						; get byte of font and point to next font data
	eora	128,x				; read byte at destination
	eorb	160,x				; read byte at destination
	staa	128,x
	stab	160,x

	pula						; get byte of font and point to next font data
	pulb						; get byte of font and point to next font data
	;restore stack pointer and interrupts
	lds		stacktmp			; restore the stack pointer
	cli							; enable interrupts
	eora	192,x				; read byte at destination
	eorb	224,x				; read byte at destination
	staa	192,x
	stab	224,x

	rts							; return
	

;**************************************************
; write two characters at once
;**************************************************
print_642:
;	ldaa	,x
	ldab	#8					; 8 bytes per character
	mul							; multiply them
	addd	#leftfont-257		; base address of font, -(" "*8 + 1) to subtract ' ' from char, -1 to adjust for using stack as a pointer
	std		leftchar			; save to character pointer
	
	ldaa	1,x
	ldab	#8					; 8 bytes per character
	mul							; multiply them
	addd	#rightfont-256		; base address of font, -(" "*8) to subtract ' ' from char
	std		rightchar

	ldd		row					; put the row to the MSB, col in LSB
	lsrb
	addd	#screen				; 7 row font data, top line of character is always blank so skip it
	std		fscreen				; transfer to x for our screen pointer

;	sts		stacktmp			; save the stack pointer
	sts		restorestack+1		; save the stack pointer, use self modifying code

twochar:
	sei							; disable interrupts
	lds		leftchar			; point the stack to the current character

	ldx		rightchar			; point to right char
	pula						; get a byte of left character
	pulb						; get another byte of left character
	eora	0,x					; add the right character to the byte.  EOR lets us use inverse fonts.
	eorb	1,x					; add the right character to the byte.
	ldx		fscreen				; point to screen	
	staa	0,x					; write out the bytes to the screen
	stab	32,x
	
	ldx		rightchar			; point to right char
	pula						; get a byte of first character
	pulb						; get another byte of first character
	eora	2,x					; add the right character to the byte
	eorb	3,x					; and again with the next byte
	ldx		fscreen				; point to screen	
	staa	64,x
	stab	96,x

	ldx		rightchar			; point to right char
	pula						; get a byte of first character
	pulb						; get another byte of first character
	eora	4,x					; add the right character to the byte
	eorb	5,x					; and again with the next byte
	ldx		fscreen				; point to screen	
	staa	128,x
	stab	160,x

	ldx		rightchar			; point to right char
	pula						; get a byte of first character
	pulb						; get another byte of first character
restorestack:
 lds		#stacktmp			; restore the stack pointer
 cli							; enable interrupts
	eora	6,x					; add the right character to the byte
	eorb	7,x					; and again with the next byte
	ldx		fscreen				; point to screen	
	staa	192,x
	stab	224,x
	

	;restore stack pointer and interrupts
;restorestack:
;	lds		stacktmp			; restore the stack pointer
;	cli							; enable interrupts

	rts							; return

	
;**************************************************
; HALF WIDTH 4x8 FONT
; Top row is always zero and not stored (336 bytes)
; characters are 4 bits wide and 7 bits high 
; (the top row is always blank)
;**************************************************
.MODULE Font
leftfont:
	fcb	$00, $00, $00, $00, $00, $00, $00, $00	; 
	fcb	$00, $20, $20, $20, $20, $00, $20, $00	;!
	fcb	$00, $50, $50, $00, $00, $00, $00, $00	;"
	fcb	$00, $20, $70, $20, $20, $70, $20, $00	;#
	fcb	$00, $20, $70, $60, $30, $70, $20, $00	;$
	fcb	$00, $50, $10, $20, $20, $40, $50, $00	;%
	fcb	$00, $20, $40, $30, $50, $50, $30, $00	;&
	fcb	$00, $20, $20, $00, $00, $00, $00, $00	;'
	fcb	$00, $10, $20, $40, $40, $40, $20, $10	;(
	fcb	$00, $40, $20, $10, $10, $10, $20, $40	;)
	fcb	$00, $20, $70, $20, $50, $00, $00, $00	;*
	fcb	$00, $00, $00, $20, $70, $20, $00, $00	;+
	fcb	$00, $00, $00, $00, $00, $00, $20, $20	;,
	fcb	$00, $00, $00, $00, $70, $00, $00, $00	;-
	fcb	$00, $00, $00, $00, $00, $00, $10, $00	;.
	fcb	$00, $10, $10, $20, $20, $40, $40, $00	;/
	fcb	$00, $20, $50, $50, $50, $50, $20, $00	;0
	fcb	$00, $20, $60, $20, $20, $20, $70, $00	;1
	fcb	$00, $20, $50, $10, $20, $40, $70, $00	;2
	fcb	$00, $70, $10, $20, $10, $50, $20, $00	;3
	fcb	$00, $50, $50, $50, $70, $10, $10, $00	;4
	fcb	$00, $70, $40, $60, $10, $50, $20, $00	;5
	fcb	$00, $10, $20, $60, $50, $50, $20, $00	;6
	fcb	$00, $70, $10, $10, $20, $20, $20, $00	;7
	fcb	$00, $20, $50, $20, $50, $50, $20, $00	;8
	fcb	$00, $20, $50, $50, $30, $20, $40, $00	;9
	fcb	$00, $00, $00, $20, $00, $00, $20, $00	;:
	fcb	$00, $00, $00, $20, $00, $00, $20, $20	;;
	fcb	$00, $00, $10, $20, $40, $20, $10, $00	;<
	fcb	$00, $00, $00, $70, $00, $70, $00, $00	;=
	fcb	$00, $00, $40, $20, $10, $20, $40, $00	;>
	fcb	$00, $20, $50, $10, $20, $00, $20, $00	;?
	fcb	$00, $20, $50, $70, $70, $40, $30, $00	;@
	fcb	$00, $30, $50, $50, $70, $50, $50, $00	;A
	fcb	$00, $60, $50, $60, $50, $50, $60, $00	;B
	fcb	$00, $30, $40, $40, $40, $40, $30, $00	;C
	fcb	$00, $60, $50, $50, $50, $50, $60, $00	;D
	fcb	$00, $70, $40, $60, $40, $40, $70, $00	;E
	fcb	$00, $70, $40, $60, $40, $40, $40, $00	;F
	fcb	$00, $30, $40, $40, $50, $50, $30, $00	;G
	fcb	$00, $50, $50, $70, $50, $50, $50, $00	;H
	fcb	$00, $70, $20, $20, $20, $20, $70, $00	;I
	fcb	$00, $30, $10, $10, $50, $50, $20, $00	;J
	fcb	$00, $50, $50, $60, $50, $50, $50, $00	;K
	fcb	$00, $40, $40, $40, $40, $40, $70, $00	;L
	fcb	$00, $50, $70, $50, $50, $50, $50, $00	;M
	fcb	$00, $60, $50, $50, $50, $50, $50, $00	;N
	fcb	$00, $20, $50, $50, $50, $50, $20, $00	;O
	fcb	$00, $60, $50, $50, $60, $40, $40, $00	;P
	fcb	$00, $20, $50, $50, $50, $50, $30, $00	;Q
	fcb	$00, $60, $50, $50, $60, $50, $50, $00	;R
	fcb	$00, $30, $40, $20, $10, $50, $20, $00	;S
	fcb	$00, $70, $20, $20, $20, $20, $20, $00	;T
	fcb	$00, $50, $50, $50, $50, $50, $20, $00	;U
	fcb	$00, $50, $50, $50, $50, $20, $20, $00	;V
	fcb	$00, $50, $50, $50, $50, $70, $50, $00	;W
	fcb	$00, $50, $50, $20, $20, $50, $50, $00	;X
	fcb	$00, $50, $50, $50, $20, $20, $20, $00	;Y
	fcb	$00, $70, $10, $20, $20, $40, $70, $00	;Z
	fcb	$00, $30, $20, $20, $20, $20, $20, $30	;[
	fcb	$00, $40, $40, $20, $20, $10, $10, $00	;\
	fcb	$00, $60, $20, $20, $20, $20, $20, $60	;]
	fcb	$00, $20, $50, $00, $00, $00, $00, $00	;^
	fcb	$00, $00, $00, $00, $00, $00, $00, $F0	;_
	fcb	$00, $20, $10, $00, $00, $00, $00, $00	;£
	fcb	$00, $00, $00, $30, $50, $50, $30, $00	;a
	fcb	$00, $40, $40, $60, $50, $50, $60, $00	;b
	fcb	$00, $00, $00, $30, $40, $40, $30, $00	;c
	fcb	$00, $10, $10, $30, $50, $50, $30, $00	;d
	fcb	$00, $00, $00, $20, $50, $60, $30, $00	;e
	fcb	$00, $10, $20, $70, $20, $20, $20, $00	;f
	fcb	$00, $00, $00, $30, $50, $50, $30, $60	;g
	fcb	$00, $40, $40, $60, $50, $50, $50, $00	;h
	fcb	$00, $20, $00, $60, $20, $20, $70, $00	;i
	fcb	$00, $10, $00, $30, $10, $10, $50, $20	;j
	fcb	$00, $40, $40, $50, $60, $50, $50, $00	;k
	fcb	$00, $60, $20, $20, $20, $20, $70, $00	;l
	fcb	$00, $00, $00, $50, $70, $50, $50, $00	;m
	fcb	$00, $00, $00, $60, $50, $50, $50, $00	;n
	fcb	$00, $00, $00, $20, $50, $50, $20, $00	;o
	fcb	$00, $00, $00, $60, $50, $50, $60, $40	;p
	fcb	$00, $00, $00, $30, $50, $50, $30, $10	;q
	fcb	$00, $00, $00, $50, $60, $40, $40, $00	;r
	fcb	$00, $00, $00, $30, $60, $30, $60, $00	;s
	fcb	$00, $00, $20, $70, $20, $20, $10, $00	;t
	fcb	$00, $00, $00, $50, $50, $50, $20, $00	;u
	fcb	$00, $00, $00, $50, $50, $20, $20, $00	;v
	fcb	$00, $00, $00, $50, $50, $70, $50, $00	;w
	fcb	$00, $00, $00, $50, $20, $20, $50, $00	;x
	fcb	$00, $00, $00, $50, $50, $50, $30, $60	;y
	fcb	$00, $00, $00, $70, $30, $60, $70, $00	;z
	fcb	$00, $10, $20, $20, $40, $20, $20, $10	;{
	fcb	$00, $20, $20, $20, $20, $20, $20, $00	;|
	fcb	$00, $40, $20, $20, $10, $20, $20, $40	;}
	fcb	$00, $50, $a0, $00, $00, $00, $00, $00	;~
	fcb	$00, $60, $90, $60, $40, $60, $90, $60	;©

	fcb $60, $60, $60, $60, $60, $60, $60, $60		; graphics characters
	fcb $60, $60, $60, $F0, $F0, $60, $60, $60
	fcb $00, $00, $00, $F0, $F0, $00, $00, $00
	fcb $60, $60, $60, $E0, $E0, $60, $60, $60
	fcb $60, $60, $60, $70, $70, $60, $60, $60
	fcb $00, $00, $00, $E0, $E0, $60, $60, $60
	fcb $00, $00, $00, $70, $70, $60, $60, $60
	fcb $00, $00, $00, $F0, $F0, $60, $60, $60
	fcb $60, $60, $60, $70, $70, $00, $00, $00
	fcb $60, $60, $60, $E0, $E0, $00, $00, $00
	fcb $60, $60, $60, $F0, $F0, $00, $00, $00
	fcb $E0, $E0, $E0, $70, $70, $60, $E0, $E0
	fcb $70, $70, $60, $E0, $E0, $60, $70, $70
	fcb $00, $F0, $90, $90, $90, $90, $F0, $00
	fcb $00, $F0, $F0, $F0, $F0, $F0, $F0, $00

	
rightfont:
	fcb	$00, $00, $00, $00, $00, $00, $00, $00	; 
	fcb	$00, $02, $02, $02, $02, $00, $02, $00	;!
	fcb	$00, $05, $05, $00, $00, $00, $00, $00	;"
	fcb	$00, $02, $07, $02, $02, $07, $02, $00	;#
	fcb	$00, $02, $07, $06, $03, $07, $02, $00	;$
	fcb	$00, $05, $01, $02, $02, $04, $05, $00	;%
	fcb	$00, $02, $04, $03, $05, $05, $03, $00	;&
	fcb	$00, $02, $02, $00, $00, $00, $00, $00	;'
	fcb	$00, $01, $02, $04, $04, $04, $02, $01	;(
	fcb	$00, $04, $02, $01, $01, $01, $02, $04	;)
	fcb	$00, $02, $07, $02, $05, $00, $00, $00	;*
	fcb	$00, $00, $00, $02, $07, $02, $00, $00	;+
	fcb	$00, $00, $00, $00, $00, $00, $02, $02	;,
	fcb	$00, $00, $00, $00, $07, $00, $00, $00	;-
	fcb	$00, $00, $00, $00, $00, $00, $01, $00	;.
	fcb	$00, $01, $01, $02, $02, $04, $04, $00	;/
	fcb	$00, $02, $05, $05, $05, $05, $02, $00	;0
	fcb	$00, $02, $06, $02, $02, $02, $07, $00	;1
	fcb	$00, $02, $05, $01, $02, $04, $07, $00	;2
	fcb	$00, $07, $01, $02, $01, $05, $02, $00	;3
	fcb	$00, $05, $05, $05, $07, $01, $01, $00	;4
	fcb	$00, $07, $04, $06, $01, $05, $02, $00	;5
	fcb	$00, $01, $02, $06, $05, $05, $02, $00	;6
	fcb	$00, $07, $01, $01, $02, $02, $02, $00	;7
	fcb	$00, $02, $05, $02, $05, $05, $02, $00	;8
	fcb	$00, $02, $05, $05, $03, $02, $04, $00	;9
	fcb	$00, $00, $00, $02, $00, $00, $02, $00	;:
	fcb	$00, $00, $00, $02, $00, $00, $02, $02	;;
	fcb	$00, $00, $01, $02, $04, $02, $01, $00	;<
	fcb	$00, $00, $00, $07, $00, $07, $00, $00	;=
	fcb	$00, $00, $04, $02, $01, $02, $04, $00	;>
	fcb	$00, $02, $05, $01, $02, $00, $02, $00	;?
	fcb	$00, $02, $05, $07, $07, $04, $03, $00	;@
	fcb	$00, $03, $05, $05, $07, $05, $05, $00	;A
	fcb	$00, $06, $05, $06, $05, $05, $06, $00	;B
	fcb	$00, $03, $04, $04, $04, $04, $03, $00	;C
	fcb	$00, $06, $05, $05, $05, $05, $06, $00	;D
	fcb	$00, $07, $04, $06, $04, $04, $07, $00	;E
	fcb	$00, $07, $04, $06, $04, $04, $04, $00	;F
	fcb	$00, $03, $04, $04, $05, $05, $03, $00	;G
	fcb	$00, $05, $05, $07, $05, $05, $05, $00	;H
	fcb	$00, $07, $02, $02, $02, $02, $07, $00	;I
	fcb	$00, $03, $01, $01, $05, $05, $02, $00	;J
	fcb	$00, $05, $05, $06, $05, $05, $05, $00	;K
	fcb	$00, $04, $04, $04, $04, $04, $07, $00	;L
	fcb	$00, $05, $07, $05, $05, $05, $05, $00	;M
	fcb	$00, $06, $05, $05, $05, $05, $05, $00	;N
	fcb	$00, $02, $05, $05, $05, $05, $02, $00	;O
	fcb	$00, $06, $05, $05, $06, $04, $04, $00	;P
	fcb	$00, $02, $05, $05, $05, $05, $03, $00	;Q
	fcb	$00, $06, $05, $05, $06, $05, $05, $00	;R
	fcb	$00, $03, $04, $02, $01, $05, $02, $00	;S
	fcb	$00, $07, $02, $02, $02, $02, $02, $00	;T
	fcb	$00, $05, $05, $05, $05, $05, $02, $00	;U
	fcb	$00, $05, $05, $05, $05, $02, $02, $00	;V
	fcb	$00, $05, $05, $05, $05, $07, $05, $00	;W
	fcb	$00, $05, $05, $02, $02, $05, $05, $00	;X
	fcb	$00, $05, $05, $05, $02, $02, $02, $00	;Y
	fcb	$00, $07, $01, $02, $02, $04, $07, $00	;Z
	fcb	$00, $03, $02, $02, $02, $02, $02, $03	;[
	fcb	$00, $04, $04, $02, $02, $01, $01, $00	;\
	fcb	$00, $06, $02, $02, $02, $02, $02, $06	;]
	fcb	$00, $02, $05, $00, $00, $00, $00, $00	;^
	fcb	$00, $00, $00, $00, $00, $00, $00, $0f	;_
	fcb	$00, $02, $01, $00, $00, $00, $00, $00	;£
	fcb	$00, $00, $00, $03, $05, $05, $03, $00	;a
	fcb	$00, $04, $04, $06, $05, $05, $06, $00	;b
	fcb	$00, $00, $00, $03, $04, $04, $03, $00	;c
	fcb	$00, $01, $01, $03, $05, $05, $03, $00	;d
	fcb	$00, $00, $00, $02, $05, $06, $03, $00	;e
	fcb	$00, $01, $02, $07, $02, $02, $02, $00	;f
	fcb	$00, $00, $00, $03, $05, $05, $03, $06	;g
	fcb	$00, $04, $04, $06, $05, $05, $05, $00	;h
	fcb	$00, $02, $00, $06, $02, $02, $07, $00	;i
	fcb	$00, $01, $00, $03, $01, $01, $05, $02	;j
	fcb	$00, $04, $04, $05, $06, $05, $05, $00	;k
	fcb	$00, $06, $02, $02, $02, $02, $07, $00	;l
	fcb	$00, $00, $00, $05, $07, $05, $05, $00	;m
	fcb	$00, $00, $00, $06, $05, $05, $05, $00	;n
	fcb	$00, $00, $00, $02, $05, $05, $02, $00	;o
	fcb	$00, $00, $00, $06, $05, $05, $06, $04	;p
	fcb	$00, $00, $00, $03, $05, $05, $03, $01	;q
	fcb	$00, $00, $00, $05, $06, $04, $04, $00	;r
	fcb	$00, $00, $00, $03, $06, $03, $06, $00	;s
	fcb	$00, $00, $02, $07, $02, $02, $01, $00	;t
	fcb	$00, $00, $00, $05, $05, $05, $02, $00	;u
	fcb	$00, $00, $00, $05, $05, $02, $02, $00	;v
	fcb	$00, $00, $00, $05, $05, $07, $05, $00	;w
	fcb	$00, $00, $00, $05, $02, $02, $05, $00	;x
	fcb	$00, $00, $00, $05, $05, $05, $03, $06	;y
	fcb	$00, $00, $00, $07, $03, $06, $07, $00	;z
	fcb	$00, $01, $02, $02, $04, $02, $02, $01	;{
	fcb	$00, $02, $02, $02, $02, $02, $02, $00	;|
	fcb	$00, $04, $02, $02, $01, $02, $02, $04	;}
	fcb	$00, $05, $0a, $00, $00, $00, $00, $00	;~
	fcb	$00, $06, $09, $06, $04, $06, $09, $06	;©
	;graphics characters 
	fcb $06, $06, $06, $06, $06, $06, $06, $06		; vertical line
	fcb $06, $06, $06, $0F, $0F, $06, $06, $06		; vertical line crossing horizontal line
	fcb $00, $00, $00, $0F, $0F, $00, $00, $00		; horizontal line
	fcb $06, $06, $06, $0E, $0E, $06, $06, $06
	fcb $06, $06, $06, $07, $07, $06, $06, $06
	fcb $00, $00, $00, $0E, $0E, $06, $06, $06
	fcb $00, $00, $00, $07, $07, $06, $06, $06
	fcb $00, $00, $00, $0F, $0F, $06, $06, $06
	fcb $06, $06, $06, $07, $07, $00, $00, $00
	fcb $06, $06, $06, $0E, $0E, $00, $00, $00
	fcb $06, $06, $06, $0F, $0F, $00, $00, $00
	fcb $0E, $0E, $0E, $07, $07, $06, $0E, $0E
	fcb $07, $07, $06, $0E, $0E, $06, $07, $07
	fcb $00, $0F, $09, $09, $09, $09, $0F, $00
	fcb $00, $0F, $0F, $0F, $0F, $0F, $0F, $00


String:	rmb	256

	end
	
